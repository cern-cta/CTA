#!/usr/bin/tcsh

if ("$1" == "-h") then
  echo "Usage :"
  echo "  gencastor [-w castor_workdir] [-c topNamespace] [-h] [-v|--version] XMIFile"
  exit
else if ("$1" == "-v" || "$1" == "--version") then
  /usr/bin/gencastor.bin --version
  exit
else if ("$1" == "-w") then
  set WORKDIR=$2
  if ("$3" == "-c") then
    set TOPNS = $4
    set XMIFILE=$5
  else
    set TOPNS = 'castor'
    set XMIFILE=$3
  endif
else
  set WORKDIR=`pwd`
  if ("$1" == "-c") then
    set TOPNS = $2
    set XMIFILE=$3
  else
    set TOPNS = 'castor'
    set XMIFILE=$1
  endif
endif
set TMPDIR=`mktemp /tmp/gencastor-XXXXXX`

rm -rf ${TMPDIR}
mkdir -p ${TMPDIR}/${TOPNS}/db
cp ${WORKDIR}/${TOPNS}/db/*.sql ${TMPDIR}/${TOPNS}/db/
grep 'CREATE OR REPLACE' ${TMPDIR}/${TOPNS}/db/oracleTrailer.sql | awk 'BEGIN { FS="[ (]+" } { print "DROP " $4, $5 ";" }' > ${TMPDIR}/${TOPNS}/db/oracleTrailer_drop.sql
grep 'CREATE OR REPLACE' ${TMPDIR}/${TOPNS}/db/postgresTrailer.sql | awk 'BEGIN { FS="[ (]+" } { print "DROP " $4, $5 ";" }' > ${TMPDIR}/${TOPNS}/db/postgresTrailer_drop.sql

(/usr/bin/gencastor.bin -o ${TMPDIR} -c ${TOPNS} --nocrashhandler ${XMIFILE} > /dev/tty) >& /dev/null
set pushdsilent
pushd ${TMPDIR}
rm -f ${TOPNS}/db/*Generated*
rm -f ${TOPNS}/db/*Trailer_drop*
foreach f (`find . -type f`)
  #echo $f
  if !(-f ${WORKDIR}/${f}) then
    if !(-d `dirname ${WORKDIR}/${f}`) then
      mkdir -p `dirname ${WORKDIR}/${f}`
    endif
    echo "Creating $f"
    cp ${TMPDIR}/${f} ${WORKDIR}/$f
  else 
    set d = `diff -q ${WORKDIR}/${f}  ${TMPDIR}/${f}`
    #echo "Diff gave --$d--"
    if ("$d" != "") then
      echo "Upgrading $f"
      cp ${TMPDIR}/${f} ${WORKDIR}/${f}
    endif
  endif
end
echo Code generation done
popd
rm -rf ${TMPDIR}
