#!/usr/bin/tcsh
#
# A wrapper script for gencastor.bin, to put files in the proper places and avoid to overwrite not modified ones.
# It also manages the generated SQL scripts.
#
# @author Castor dev team, castor-dev@cern.ch
#
# @(#)$RCSfile: gencastor,v $ $Revision: 1.14 $ $Release$ $Date: 2006/08/07 14:44:59 $ $Author: itglp $
# 

if ("$1" == "-h") then
  echo "Usage :"
  echo "  gencastor [-w castor_workdir] [-c topNamespace] [-h] [-v|--version] XMIFile"
  exit
else if ("$1" == "-v" || "$1" == "--version") then
  /usr/bin/gencastor.bin --version
  exit
else if ("$1" == "-w") then
  set WORKDIR=$2
  if ("$3" == "-c") then
    set TOPNS = $4
    set XMIFILE=$5
  else
    set TOPNS = 'castor'
    set XMIFILE=$3
  endif
else
  set WORKDIR=`pwd`
  if ("$1" == "-c") then
    set TOPNS = $2
    set XMIFILE=$3
  else
    set TOPNS = 'castor'
    set XMIFILE=$1
  endif
endif

set TMPDIR=`mktemp /tmp/gencastor-XXXXXX`
rm -rf ${TMPDIR}
mkdir -p ${TMPDIR}/${TOPNS}/db
cp ${WORKDIR}/${TOPNS}/db/*.sql ${TMPDIR}/${TOPNS}/db/
grep 'CREATE OR REPLACE' ${TMPDIR}/${TOPNS}/db/oracleTrailer.sql | grep -v 'TRIGGER' | grep -v I_CastorFile_fileIdNsHost | awk 'BEGIN { FS="[ (]+" } { print "DROP " $4, $5 ";" }' > ${TMPDIR}/${TOPNS}/db/oracleTrailer_drop.sql
grep 'CREATE OR REPLACE' ${TMPDIR}/${TOPNS}/db/postgresTrailer.sql | grep -v 'TRIGGER' | grep -v I_CastorFile_fileIdNsHost | awk 'BEGIN { FS="[ (]+" } { print "DROP " $4, $5 ";" }' > ${TMPDIR}/${TOPNS}/db/postgresTrailer_drop.sql

# hack to prevent Umbrello from overwriting the original XMI file
cp ${XMIFILE} ${TMPDIR}
set XMITOUSE=`basename ${XMIFILE}`

(/usr/bin/gencastor.bin -o ${TMPDIR} -c ${TOPNS} --nocrashhandler ${TMPDIR}/${XMITOUSE} > /dev/tty) >& /dev/null

sed 's/^END;/END;\n\//' ${TMPDIR}/${TOPNS}/db/${TOPNS}_oracle_create.sql | sed 's/^\(END castor[a-zA-Z]*;\)/\1\n\//' | sed 's/\(CREATE OR REPLACE TYPE .*\)$/\1\n\//' > ${TMPDIR}/${TOPNS}/db/${TOPNS}_oracle_create.sqlplus

set pushdsilent
pushd ${TMPDIR}
rm -f ${TOPNS}/db/*Generated*
rm -f ${TOPNS}/db/*Trailer_drop*
rm -f ${XMITOUSE}

foreach f (`find . -type f`)
  #echo $f
  if !(-f ${WORKDIR}/${f}) then
    if !(-d `dirname ${WORKDIR}/${f}`) then
      mkdir -p `dirname ${WORKDIR}/${f}`
    endif
    echo "Creating $f"
    cp ${TMPDIR}/${f} ${WORKDIR}/$f
  else
    if (-f ${WORKDIR}/${f}_clone) then
      echo Merging clone before upgrading $f
      `dirname $0`/codemerge.py ${TMPDIR}/${f} ${WORKDIR}/${f}_clone ${TMPDIR}/${f}_merge
      mv ${TMPDIR}/${f}_merge ${TMPDIR}/${f}
    endif
    set d = `diff -q ${WORKDIR}/${f} ${TMPDIR}/${f}`
    #echo "Diff gave --$d--"
    if ("$d" != "") then
      echo "Upgrading $f"
      cp ${TMPDIR}/${f} ${WORKDIR}/${f}
    endif
  endif
end
echo Code generation done
popd
rm -rf ${TMPDIR}
