# Guess the CASTOR version from the various criteria in the environement:
# - Get MINOR_CASTOR_VERSION and MAJOR_CASTOR_VERSION from the environement, if 
# they exists.
# - If not, guess them from the file debian/changelog
# - The major is expected in the form: A.B (fixed to 2.1 for ages)
# - The minor is expected to be in the for C.D.
# - The package version (in rpm) will be A.B.C-D
# - If the variable VCS_VERSION is defined (from command line:
#  "cmake -DVCS_VERSION:STRING=git-6789abc")

# Extract the versions from changelog anyway
SET(VERSION_RE "^castor \\(([0-9]+)\\.([0-9]+)\\.([0-9]+)-([0-9]+)\\).*")
FILE(STRINGS debian/changelog CHANGELOG_HEAD
     REGEX ${VERSION_RE}
     LIMIT_COUNT 1)
# MESSAGE("Changelog line: ${CHANGELOG_HEAD}")
# MESSAGE("VERSION_RE ${VERSION_RE}")
STRING(REGEX REPLACE "${VERSION_RE}" "\\1" A ${CHANGELOG_HEAD})
STRING(REGEX REPLACE "${VERSION_RE}" "\\2" B ${CHANGELOG_HEAD})
STRING(REGEX REPLACE "${VERSION_RE}" "\\3" C ${CHANGELOG_HEAD})
STRING(REGEX REPLACE "${VERSION_RE}" "\\4" D ${CHANGELOG_HEAD})
# MESSAGE("A ${A} B ${B} C ${C} D ${D}")

# Get versions from environment, if availble
# MAJOR
IF(NOT $ENV{MAJOR_CASTOR_VERSION} STREQUAL "")
  SET(MAJOR_CASTOR_VERSION $ENV{MAJOR_CASTOR_VERSION})
  MESSAGE(STATUS "Got MAJOR_CASTOR_VERSION from environment: ${MAJOR_CASTOR_VERSION}")
ELSE(NOT $ENV{MAJOR_CASTOR_VERSION} STREQUAL "")
  SET(MAJOR_CASTOR_VERSION "${A}.${B}")
  MESSAGE(STATUS "Got MAJOR_CASTOR_VERSION from debian/changelog: ${MAJOR_CASTOR_VERSION}")
ENDIF(NOT $ENV{MAJOR_CASTOR_VERSION} STREQUAL "")

# MINOR
IF(NOT $ENV{MINOR_CASTOR_VERSION} STREQUAL "")
  SET(MINOR_CASTOR_VERSION $ENV{MINOR_CASTOR_VERSION})
  MESSAGE(STATUS "Got MINOR_CASTOR_VERSION from environment: ${MINOR_CASTOR_VERSION}")
ELSE(NOT $ENV{MINOR_CASTOR_VERSION} STREQUAL "")
  SET(MINOR_CASTOR_VERSION "${C}.${D}")
  MESSAGE(STATUS "Got MINOR_CASTOR_VERSION from debian/changelog: ${MINOR_CASTOR_VERSION}")
ENDIF(NOT $ENV{MINOR_CASTOR_VERSION} STREQUAL "")

# Change the release number if VCS version is provided
IF(DEFINED VCS_VERSION)
  STRING(REGEX REPLACE "\\..*" ".${VCS_VERSION}" MINOR_CASTOR_VERSION ${MINOR_CASTOR_VERSION})
  MESSAGE(STATUS "Replaced VCS_VERSION in MINOR_CASTOR_VERSION: ${MINOR_CASTOR_VERSION}")
ENDIF(DEFINED VCS_VERSION)

# Generate derived version from result. Almost all combinaisons are needed at some point!
STRING(REGEX REPLACE "(.*)\\..*" "${MAJOR_CASTOR_VERSION}.\\1" CASTOR_VERSION ${MINOR_CASTOR_VERSION})
STRING(REGEX REPLACE ".*\\." "" CASTOR_RELEASE ${MINOR_CASTOR_VERSION})
STRING(REGEX REPLACE "\\..*" "" MAJOR_CASTOR_VERSION_TOP ${MAJOR_CASTOR_VERSION})
STRING(REGEX REPLACE ".*\\." "" MAJOR_CASTOR_VERSION_BOTTOM ${MAJOR_CASTOR_VERSION})
STRING(REGEX REPLACE "\\..*" "" MINOR_CASTOR_VERSION_TOP ${MINOR_CASTOR_VERSION})
STRING(REGEX REPLACE ".*\\." "" MINOR_CASTOR_VERSION_BOTTOM ${MINOR_CASTOR_VERSION})
MESSAGE(STATUS "Generated derived versions: CASTOR_VERSION: ${CASTOR_VERSION} CASTOR_RELEASE: ${CASTOR_RELEASE}")
MESSAGE(STATUS "MAJOR_CASTOR_VERSION_TOP=${MAJOR_CASTOR_VERSION_TOP} MAJOR_CASTOR_VERSION_BOTTOM=${MAJOR_CASTOR_VERSION_BOTTOM}")
MESSAGE(STATUS "MINOR_CASTOR_VERSION_TOP=${MINOR_CASTOR_VERSION_TOP} MINOR_CASTOR_VERSION_BOTTOM=${MINOR_CASTOR_VERSION_BOTTOM}")

# Generate the compilation variables, if needed
IF(NOT DEFINED COMPILING_NOSTK)
  SET(COMPILING_NOSTK 0)
  MESSAGE(STATUS "Setting option COMPILING_NOSTK to default (0). Override with option -DCOMPILING_NOSTK:STRING=1")
ENDIF(NOT DEFINED COMPILING_NOSTK)
IF(NOT DEFINED COMPILING_CLIENT)
  SET(COMPILING_CLIENT 0)
  MESSAGE(STATUS "Setting option COMPILING_CLIENT to default (0). Override with option -DCOMPILING_CLIENT:STRING=1")
ENDIF(NOT DEFINED COMPILING_CLIENT)
